include "core:api",
        "core:model",
        "core:spi",
        "core:server",
        "clients:cli",
        "clients:web",
        "runtimes:default:provider",
        "runtimes:default:starter",
        "runtimes:scripted",
        "containers:bare",
        "containers:ec2",
        "services:dosgi",
        "site",
        'examples:jaxws:client',
        'examples:jaxws:server'

rootProject.name = "org.ops4j.pax.clapper"

validateProject(rootProject, "")

def validateProject(project, parentName)
{
  assert project.projectDir.isDirectory()
  String srcDir = "$project.projectDir/src/main/java"
  if( new File(srcDir).exists() )
  {
    String fileBaseName = project.name.replaceAll( "\\p{Upper}" ) { "-${it.toLowerCase()}" }
    project.buildFileName = "${fileBaseName}.gradle"
    assert project.buildFile.isFile()
  }
  if( parentName.endsWith('s') )
  {
    parentName = parentName.substring(0, parentName.length() - 1)
  }
  if( parentName.length() > 0 )
  {
    project.name = parentName + "." + project.name
  }
  println "Project: " + project.name
  project.children.each { child ->
    validateProject(child, project.name)
  }
}